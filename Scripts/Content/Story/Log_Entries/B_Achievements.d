// ДОСТИЖЕНИЯ ----------------------------------------------


const string TOPIC_Achieve = "Достижения";
	const string TOPIC_Achieve_Elena_CapriciousGirl_Title = "КАПРИЗНАЯ ДЕВИЦА";
	const string TOPIC_Achieve_Elena_CapriciousGirl_Content = "заставили Брайана переплавить кольца обратно.";
		var int ACHIEV_Elena_CapriciousGirl;
	const string TOPIC_Achieve_Elena_Shopoholic_Title = "ШОПОГОЛИК";
	const string TOPIC_Achieve_Elena_Shopoholic_Content = "нашли все возможные варианты подарка для родителей.";
		var int ACHIEV_Elena_Shopoholic;
	const string TOPIC_Achieve_Till_TeaMaster_Title = "ТРАВНИК";
	const string TOPIC_Achieve_Till_TeaMaster_Content = "собрали весь горичай.";
		var int ACHIEV_Till_TeaMaster;
	const string TOPIC_Achieve_Till_Accoucheur_Title = "АКУШЕР";
	const string TOPIC_Achieve_Till_Accoucheur_Content = "успешно приняли роды у Крестика.";
		var int ACHIEV_Till_Accoucheur;
	const string TOPIC_Achieve_Talbin_Furrier_Title = "СКОРНЯК";
	const string TOPIC_Achieve_Talbin_Furrier_Content = "собрали все виды шкур.";
		var int ACHIEV_Talbin_Furrier;
	const string TOPIC_Achieve_Talbin_Ranger_Title = "СЛЕДОПЫТ";
	const string TOPIC_Achieve_Talbin_Ranger_Content = "нашли все следы белого волка.";
		var int ACHIEV_Talbin_Ranger;
	const string TOPIC_Achieve_Rupert_SwordMaster_Title = "МАСТЕР МЕЧА";
	const string TOPIC_Achieve_Rupert_SwordMaster_Content = "получили рудный меч и оставили его себе.";
		var int ACHIEV_Rupert_SwordMaster;
	const string TOPIC_Achieve_Rupert_Waster_Title = "МОТ";
	const string TOPIC_Achieve_Rupert_Waster_Content = "пропили с Регисом все заработанные деньги.";
		var int ACHIEV_Rupert_Waster;
	const string TOPIC_Achieve_Erol_Archeologist_Title = "АРХЕОЛОГ";
	const string TOPIC_Achieve_Erol_Archeologist_Content = "собрали все каменные таблички.";
		var int ACHIEV_Erol_Archeologist;
	const string TOPIC_Achieve_Erol_Decoder_Title = "ДЕШИФРАТОР";
	const string TOPIC_Achieve_Erol_Decoder_Content = "расшифровали систему счета зодчих.";
		var int ACHIEV_Erol_Decoder;
	const string TOPIC_Achieve_Odo_Purveyor_Title = "БРАТ МИЛОСЕРДИЯ";
	const string TOPIC_Achieve_Odo_Purveyor_Content = "кормили Педро каждый день.";
		var int ACHIEV_Odo_Purveyor;
	const string TOPIC_Achieve_Odo_Cleaner_Title = "БОРЕЦ ЗА ЧИСТОТУ";
	const string TOPIC_Achieve_Odo_Cleaner_Content = "продолжили подметать после начала конкурса.";
		var int ACHIEV_Odo_Cleaner;
	const string TOPIC_Achieve_Sarah_Thorn_Title = "ЗАНОЗА";
	const string TOPIC_Achieve_Sarah_Thorn_Content = "трижды напомнили Кантару, за чьим прилавком он стоит.";
		var int ACHIEV_Sarah_Thorn;
	const string TOPIC_Achieve_Sarah_GhostWhisper_Title = "ГОВОРЯЩАЯ С ПРИЗРАКАМИ";
	const string TOPIC_Achieve_Sarah_GhostWhisper_Content = "повидались с Берни.";
		var int ACHIEV_Sarah_GhostWhisper;

	const string TOPIC_Achieve_Degustator_Title = "ДЕГУСТАТОР";
	const string TOPIC_Achieve_Degustator_Content = "перепробовали все несмертельные грибы.";
		var int ACHIEV_Degustator;
	const string TOPIC_Achieve_Pioneer_Title = "ПЕРВООТКРЫВАТЕЛЬ";
	const string TOPIC_Achieve_Pioneer_Content = "нашли гриб, о котором Константино не знает.";
		var int ACHIEV_Pioneer;
	const string TOPIC_Achieve_Novice_Title = "НАЧИНАЮЩИЙ ГРИБНИК";
	const string TOPIC_Achieve_Novice_Content = "собрали и сдали Константино 10 грибов.";
		var int ACHIEV_Novice;
	const string TOPIC_Achieve_Expert_Title = "ОПЫТНЫЙ ГРИБНИК";
	const string TOPIC_Achieve_Expert_Content = "собрали и сдали Константино 100 грибов.";
		var int ACHIEV_Expert;
	const string TOPIC_Achieve_GreatMushroomer_Title = "ВЕЛИКИЙ ГРИБНИК";
	const string TOPIC_Achieve_GreatMushroomer_Content = "собрали и сдали Константино невероятное количество грибов - целых 500.";
		var int ACHIEV_GreatMushroomer;
	const string TOPIC_Achieve_Mycologist_Title = "МИКОЛОГ";
	const string TOPIC_Achieve_Mycologist_Content = "собрали и сдали Константино все виды грибов.";
		var int ACHIEV_Mycologist;
	const string TOPIC_Achieve_Governor_Title = "28 ГУБЕРНАТОРОВ";
	const string TOPIC_Achieve_Governor_Content = "так легенда не врет?";
		var int ACHIEV_Governor;


	const string TOPIC_Achieve_Polytheist_Title = "ПОЛИТЕИСТ";
	const string TOPIC_Achieve_Polytheist_Content = "молились всем трем богам.";
		var int ACHIEV_Polytheist;
	const string TOPIC_Achieve_Monotheist_Title = "МОНОТЕИСТ";
	const string TOPIC_Achieve_Monotheist_Content = "поклонялись только одному богу.";
		var int ACHIEV_Monotheist;
	const string TOPIC_Achieve_Atheist_Title = "АТЕИСТ";
	const string TOPIC_Achieve_Atheist_Content = "ни разу не помолились и не пожертвовали ни одному богу.";
		var int ACHIEV_Atheist;
	
	const string TOPIC_Achieve_Sleepless_Title = "НЕСПЯЩИЙ";
	const string TOPIC_Achieve_Sleepless_Content = "не спали трое суток.";
		var int ACHIEV_Sleepless;
	const string TOPIC_Achieve_Orentalist_Title = "РУДОВЕД";
	const string TOPIC_Achieve_Orentalist_Content = "нашли и принесли Гараду 5 кусков руды.";
		var int ACHIEV_Orentalist;
	const string TOPIC_Achieve_ArtFan_Title = "ЦЕНИТЕЛЬ ПРЕКРАСНОГО";
	const string TOPIC_Achieve_ArtFan_Content = "нашли все рисунки.";
		var int ACHIEV_ArtFan;
	const string TOPIC_Achieve_Listener_Title = "СЛУШАТЕЛЬ";
	const string TOPIC_Achieve_Listener_Content = "послушали все байки и истории.";
		var int ACHIEV_Listener;
	const string TOPIC_Achieve_Librarian_Title = "БИБЛИОТЕКАРЬ";
	const string TOPIC_Achieve_Librarian_Content = "прочли все новые книги.";
		var int ACHIEV_Librarian;
	const string TOPIC_Achieve_Archivist_Title = "АРХИВАРИУС";
	const string TOPIC_Achieve_Archivist_Content = "прочли все старые книги.";
		var int ACHIEV_Archivist;
	const string TOPIC_Achieve_Fashionista_Title = "МОДНИЦА";
	const string TOPIC_Achieve_Fashionista_Content = "перемерили все платья у Маттео.";
		var int ACHIEV_Fashionista;

	const string TOPIC_Achieve_MultiQuest_Title = "МНОГОЗАДАЧНОСТЬ";
	const string TOPIC_Achieve_MultiQuest_Content = "успешно завершили все побочные задания до конца конкурса.";
		var int ACHIEV_MultiQuest;
	const string TOPIC_Achieve_GoalSeeking_Title = "ЦЕЛЕУСТРЕМЛЕННОСТЬ";
	const string TOPIC_Achieve_GoalSeeking_Content = "не отвлекались на побочные задания на пути к победе в конкурсе.";
		var int ACHIEV_GoalSeeking;
	const string TOPIC_Achieve_Dutiful_Title = "ОТВЕТСТВЕННОСТЬ";
	const string TOPIC_Achieve_Dutiful_Content = "успешно завершили все побочные задания после окончания конкурса.";
		var int ACHIEV_Dutiful;

	const string TOPIC_Achieve_SnowStudio_Title = "ФАНАТ SNOWSTUDIO";
	const string TOPIC_Achieve_SnowStudio_Content = "встретились со всеми актерами SnowStudio.";
		var int ACHIEV_SnowStudio;


//-----------------------------------------
var int ACHIEV_POS_Y;
func void B_GiveAchievement(var string title, var string content)
{
	Log_CreateTopic(TOPIC_Achieve, LOG_NOTE);
	Log_AddEntry(TOPIC_Achieve,CS3(title, ": ", content));
	AI_Snd_Play(hero,"SFX_INNOSEYE");
	AI_PrintScreen("Получено достижение: ", 2, 2 + ACHIEV_POS_Y*2, FONT_ScreenSmall, 5);
	AI_PrintScreen(title, 2, 5 + ACHIEV_POS_Y*2, FONT_ScreenSmall, 5);
	ACHIEV_POS_Y += 5;
};

//-----------------------------------------
func void B_GiveAchievement_Elena_CapriciousGirl()
{
	if (!ACHIEV_Elena_CapriciousGirl)	{
		if (C_HeroIs_Elena() && MIS_Elena_Present_BrianReturn)	{
			B_GiveAchievement(TOPIC_Achieve_Elena_CapriciousGirl_Title, TOPIC_Achieve_Elena_CapriciousGirl_Content);
			ACHIEV_Elena_CapriciousGirl = TRUE;
		};
	};
};
func void B_GiveAchievement_Elena_Shopoholic()
{
	if (!ACHIEV_Elena_Shopoholic)	{
		if (C_HeroIs_Elena() && (MIS_Elena_Present_FoundCnt == 3))	{
			B_GiveAchievement(TOPIC_Achieve_Elena_Shopoholic_Title, TOPIC_Achieve_Elena_Shopoholic_Content);
			ACHIEV_Elena_Shopoholic = TRUE;
		};
	};
};
func void B_GiveAchievement_Erol_Archeologist()
{
	if (!ACHIEV_Erol_Archeologist)	{
		if (C_HeroIs_Erol() && (MIS_Erol_Plates_GivenCnt >= 15))	{
			B_GiveAchievement(TOPIC_Achieve_Erol_Archeologist_Title, TOPIC_Achieve_Erol_Archeologist_Content);
			ACHIEV_Erol_Archeologist = TRUE;
		};
	};
};
func void B_GiveAchievement_Erol_Decoder()
{
	if (!ACHIEV_Erol_Decoder)	{
		if (C_HeroIs_Erol() && MIS_Erol_Plates_NumbersSolved)	{
			B_GiveAchievement(TOPIC_Achieve_Erol_Decoder_Title, TOPIC_Achieve_Erol_Decoder_Content);
			ACHIEV_Erol_Decoder = TRUE;
		};
	};
};
func void B_GiveAchievement_Odo_Purveyor()
{
	if (!ACHIEV_Odo_Purveyor)	{
		if (C_HeroIs_Odo() && (Pedro_FedCnt >= 7) && (Wld_GetDay() > Competition_LastDay))	{
			B_GiveAchievement(TOPIC_Achieve_Odo_Purveyor_Title, TOPIC_Achieve_Odo_Purveyor_Content);
			ACHIEV_Odo_Purveyor = TRUE;
		};
	};
};
func void B_GiveAchievement_Odo_Cleaner()
{
	if (!ACHIEV_Odo_Cleaner)	{
		if (C_HeroIs_Odo() && (Use_Broom_Odo_Cnt > 1))	{
			B_GiveAchievement(TOPIC_Achieve_Odo_Cleaner_Title, TOPIC_Achieve_Odo_Cleaner_Content);
			ACHIEV_Odo_Cleaner = TRUE;
		};
	};
};
func void B_GiveAchievement_Rupert_SwordMaster()
{
	if (!ACHIEV_Rupert_SwordMaster)	{
		if (C_HeroIs_Rupert() && Npc_HasItems(hero,ItMw_1H_Sword_Lehmar) && MIS_Rupert_Debts_Paid[Creditor_Lehmar])	{
			B_GiveAchievement(TOPIC_Achieve_Rupert_SwordMaster_Title, TOPIC_Achieve_Rupert_SwordMaster_Content);
			ACHIEV_Rupert_SwordMaster = TRUE;
		};
	};
};
func void B_GiveAchievement_Rupert_Waster()
{
	if (!ACHIEV_Rupert_Waster)	{
		if (C_HeroIs_Rupert() && (Rupert_Binge == -2))	{
			B_GiveAchievement(TOPIC_Achieve_Rupert_Waster_Title, TOPIC_Achieve_Rupert_Waster_Content);
			ACHIEV_Rupert_Waster = TRUE;
		};
	};
};
func void B_GiveAchievement_Sarah_GhostWhisper()
{
	if (!ACHIEV_Sarah_GhostWhisper)	{
		if (C_HeroIs_Sarah() && MIS_Sarah_OldFriends_BernyMet)	{
			B_GiveAchievement(TOPIC_Achieve_Sarah_GhostWhisper_Title, TOPIC_Achieve_Sarah_GhostWhisper_Content);
			ACHIEV_Sarah_GhostWhisper = TRUE;
		};
	};
};
func void B_GiveAchievement_Sarah_Thorn()
{
	if (!ACHIEV_Sarah_Thorn)	{
		if (C_HeroIs_Sarah() && (DIA_Canthar_MR_Hello_Sarah_Counter >= 3))	{
			B_GiveAchievement(TOPIC_Achieve_Sarah_Thorn_Title, TOPIC_Achieve_Sarah_Thorn_Content);
			ACHIEV_Sarah_Thorn = TRUE;
		};
	};
};
func void B_GiveAchievement_Talbin_Furrier()
{
	if (!ACHIEV_Talbin_Furrier)	{
		if (C_HeroIs_Talbin()
			 && Npc_HasItems(hero,ItAt_Addon_KeilerFur) && Npc_HasItems(hero,ItAt_IceWolfFur) && Npc_HasItems(hero,ItAt_ShadowFur)
			 && Npc_HasItems(hero,ItAt_SheepFur) && Npc_HasItems(hero,ItAt_WargFur) //&& Npc_HasItems(hero,ItAt_TrollFur) невозможно получить
			 && Npc_HasItems(hero,ItAt_WolfFur))	{
			B_GiveAchievement(TOPIC_Achieve_Talbin_Furrier_Title, TOPIC_Achieve_Talbin_Furrier_Content);
			ACHIEV_Talbin_Furrier = TRUE;
		};
	};
};
func void B_GiveAchievement_Talbin_Ranger()
{
	if (!ACHIEV_Talbin_Ranger)	{
		if (C_HeroIs_Talbin()
			 && EVT_WhiteWolf_LakeTrace_Once && EVT_WhiteWolf_StairsTrace_Once && EVT_WhiteWolf_Fur_Once && EVT_WhiteWolf_Fight_Once)	{
			B_GiveAchievement(TOPIC_Achieve_Talbin_Ranger_Title, TOPIC_Achieve_Talbin_Ranger_Content);
			ACHIEV_Talbin_Ranger = TRUE;
		};
	};
};
func void B_GiveAchievement_Till_TeaMaster()
{
	if (!ACHIEV_Till_TeaMaster)	{
		if (C_HeroIs_Till() && (Npc_HasItems(hero,ItPl_BurnTea) >= 5))	{
			B_GiveAchievement(TOPIC_Achieve_Till_TeaMaster_Title, TOPIC_Achieve_Till_TeaMaster_Content);
			ACHIEV_Till_TeaMaster = TRUE;
		};
	};
};
func void B_GiveAchievement_Till_Accoucheur()
{
	if (!ACHIEV_Till_Accoucheur)	{
		if (C_HeroIs_Till() && MIS_Till_Pet_ChildBorn && (MIS_Till_Pet_BirthPlace == MIS_Till_Pet_BirthPlace_Cave))	{
			B_GiveAchievement(TOPIC_Achieve_Till_Accoucheur_Title, TOPIC_Achieve_Till_Accoucheur_Content);
			ACHIEV_Till_Accoucheur = TRUE;
		};
	};
};

//-----------------------------------------

func void B_GiveAchievement_Degustator()
{
	if (!ACHIEV_Degustator)	{
		if (MR_Eaten[MR_01] && MR_Eaten[MR_02] && MR_Eaten[MR_Bottle] && MR_Eaten[MR_Branch] && MR_Eaten[MR_BrownOiler] && MR_Eaten[MR_DragonTears]
			 && MR_Eaten[MR_Foggy] && MR_Eaten[MR_Governor] && MR_Eaten[MR_Ivy] && MR_Eaten[MR_Keil] && MR_Eaten[MR_LadysEar] && MR_Eaten[MR_NightSparkles]
			 && MR_Eaten[MR_Oyster] && MR_Eaten[MR_Piny] && MR_Eaten[MR_Red] && MR_Eaten[MR_Stone] && MR_Eaten[MR_Sunrise] && MR_Eaten[MR_Trident])	{
			B_GiveAchievement(TOPIC_Achieve_Degustator_Title, TOPIC_Achieve_Degustator_Content);
			ACHIEV_Degustator = TRUE;
		};
	};
};
func void B_GiveAchievement_Pioneer()
{
	if (!ACHIEV_Pioneer)	{
		if (Competition_Bonus > 0)	{
			B_GiveAchievement(TOPIC_Achieve_Pioneer_Title, TOPIC_Achieve_Pioneer_Content);
			ACHIEV_Pioneer = TRUE;
		};
	};
};
func void B_GiveAchievement_Governor()
{
	if (!ACHIEV_Governor)	{
		B_GiveAchievement(TOPIC_Achieve_Governor_Title, TOPIC_Achieve_Governor_Content);
		ACHIEV_Governor = TRUE;
	};
};
func void B_GiveAchievement_Mushroomer()
{
	if (ACHIEV_GreatMushroomer)	{
		return;
	};
	var int sum;	sum = 0;
	var int i;	i = 0;
	while (i < MR_Count);
		sum += MEM_ReadStatArr(MR_Counter, i);
		i += 1;
	end;
	if (!ACHIEV_Novice)	{
		if (sum >= 10)	{
			B_GiveAchievement(TOPIC_Achieve_Novice_Title, TOPIC_Achieve_Novice_Content);
			ACHIEV_Novice = TRUE;
		};
	};
	if (!ACHIEV_Expert)	{
		if (sum >= 100)	{
			B_GiveAchievement(TOPIC_Achieve_Expert_Title, TOPIC_Achieve_Expert_Content);
			ACHIEV_Expert = TRUE;
		};
	};
	if (!ACHIEV_GreatMushroomer)	{
		if (sum >= 500)	{
			B_GiveAchievement(TOPIC_Achieve_GreatMushroomer_Title, TOPIC_Achieve_GreatMushroomer_Content);
			ACHIEV_GreatMushroomer = TRUE;
		};
	};
};
func void B_GiveAchievement_Mycologist()
{
	var int sum;	sum = 0;
	var int i;	i = 0;
	while (i < MR_Count);
		if (MEM_ReadStatArr(MR_Counter, i) > 0)	{
			sum += 1;
		};
		i += 1;
	end;
	if (!ACHIEV_Mycologist)	{
		if (sum == MR_Count)	{
			B_GiveAchievement(TOPIC_Achieve_Mycologist_Title, TOPIC_Achieve_Mycologist_Content);
			ACHIEV_Mycologist = TRUE;
		};
	};
};

//-----------------------------------------

func void B_GiveAchievement_Xtheist()
{
	if (!ACHIEV_Atheist && !ACHIEV_Monotheist && !ACHIEV_Polytheist)	{
		if (Wld_GetDay() >= Competition_LastDay)	{ 
			if ((PrayCounter_Innos == 0) && (PrayCounter_Adanos == 0) && (PrayCounter_Beliar == 0))	{
				if (PATRONGOD == 0)	{
					B_GiveAchievement(TOPIC_Achieve_Atheist_Title, TOPIC_Achieve_Atheist_Content);
				};
				ACHIEV_Atheist = TRUE;
			};
			if ((PATRONGOD == PATRONGOD_Innos) && (PrayCounter_Innos > 0) && (PrayCounter_Adanos == 0) && (PrayCounter_Beliar == 0))	{
				B_GiveAchievement(TOPIC_Achieve_Monotheist_Title, TOPIC_Achieve_Monotheist_Content);
				ACHIEV_Monotheist = TRUE;
			};
			if ((PATRONGOD == PATRONGOD_Adanos) && (PrayCounter_Innos == 0) && (PrayCounter_Adanos > 0) && (PrayCounter_Beliar == 0))	{
				B_GiveAchievement(TOPIC_Achieve_Monotheist_Title, TOPIC_Achieve_Monotheist_Content);
				ACHIEV_Monotheist = TRUE;
			};
			if ((PATRONGOD == PATRONGOD_Beliar) && (PrayCounter_Innos == 0) && (PrayCounter_Adanos == 0) && (PrayCounter_Beliar > 0))	{
				B_GiveAchievement(TOPIC_Achieve_Monotheist_Title, TOPIC_Achieve_Monotheist_Content);
				ACHIEV_Monotheist = TRUE;
			};
			if ((PrayCounter_Innos > 0) && (PrayCounter_Adanos > 0) && (PrayCounter_Beliar > 0))	{
				B_GiveAchievement(TOPIC_Achieve_Polytheist_Title, TOPIC_Achieve_Polytheist_Content);
				ACHIEV_Polytheist = TRUE;
			};
		};
	};
};

//-----------------------------------------

func void B_GiveAchievement_Sleepless()
{
	if (!ACHIEV_Sleepless)	{
		if (Wld_GetFullTime() - HERO_LastSleepTime > 3 * 24 * 60)	{
			B_GiveAchievement(TOPIC_Achieve_Sleepless_Title, TOPIC_Achieve_Sleepless_Content);
			ACHIEV_Sleepless = TRUE;
		};
	};
};
func void B_GiveAchievement_Orentalist()
{
	if (!ACHIEV_Orentalist)	{
		if (Harad_TradeNugget_Cnt == 5)	{
			B_GiveAchievement(TOPIC_Achieve_Orentalist_Title, TOPIC_Achieve_Orentalist_Content);
			ACHIEV_Orentalist = TRUE;
		};
	};
};
func void B_GiveAchievement_ArtFan()
{
	if (!ACHIEV_ArtFan)	{
		if (LOOK_POSTER_WOMAN_Once && LOOK_POSTER_WOMAN2_Once && LOOK_POSTER_WOMAN4_Once)	{
			B_GiveAchievement(TOPIC_Achieve_ArtFan_Title, TOPIC_Achieve_ArtFan_Content);
			ACHIEV_ArtFan = TRUE;
		};
	};
};
func void B_GiveAchievement_Listener()
{
	if (!ACHIEV_Listener)	{
		Listen_Tale_Count += 1;
		var int Listen_Tale_Count_Full;	Listen_Tale_Count_Full = Listen_Tale_Count_All;
		if (C_HeroIs_Elena())	{
			Listen_Tale_Count_Full += Listen_Tale_Count_Elena;
		};
		if (Listen_Tale_Count == Listen_Tale_Count_Full)	{
			B_GiveAchievement(TOPIC_Achieve_Listener_Title, TOPIC_Achieve_Listener_Content);
			ACHIEV_Listener = TRUE;
		};
	};
};
func void B_GiveAchievement_Librarian()
{
	if (!ACHIEV_Librarian)	{
		Read_Books_Count += 1;
		var int Read_Books_Count_Full;	Read_Books_Count_Full = Read_Books_Count_All;
		if (C_HeroIs_Odo())	{
			Read_Books_Count_Full += 1;
		};
		if (Read_Books_Count == Read_Books_Count_Full)	{
			B_GiveAchievement(TOPIC_Achieve_Librarian_Title, TOPIC_Achieve_Librarian_Content);
			ACHIEV_Librarian = TRUE;
		};
	};
};
func void B_GiveAchievement_Archivist()
{
	if (!ACHIEV_Archivist)	{
		Read_BooksOld_Count += 1;
		if (Read_BooksOld_Count == Read_BooksOld_Count_All)	{
			B_GiveAchievement(TOPIC_Achieve_Archivist_Title, TOPIC_Achieve_Archivist_Content);
			ACHIEV_Archivist = TRUE;
		};
	};
};
func void B_GiveAchievement_Fashionista()
{
	if (!ACHIEV_Fashionista)	{
		if (Matteo_ClothWear_Counter == 5)	{
			B_GiveAchievement(TOPIC_Achieve_Fashionista_Title, TOPIC_Achieve_Fashionista_Content);
			ACHIEV_Fashionista = TRUE;
		};
	};
};

func void B_GiveAchievement_MultiQuest_Dutiful()
{
	if (!ACHIEV_MultiQuest && !ACHIEV_Dutiful)	{
		if (C_HeroIs_Elena() && (MIS_Elena_Present != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Erol() && (MIS_Erol_Plates != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Odo() && (MIS_Odo_Herbs != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Odo() && (MIS_Odo_Straw != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Rupert() && (MIS_Rupert_Debts != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Sarah() && (MIS_Sarah_BadHabit != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Sarah() && (MIS_Sarah_OldFriends != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Talbin() && (MIS_Talbin_Hunt != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Talbin() && (MIS_Talbin_WhiteWolf != LOG_SUCCESS))	{	return;	};
		if (C_HeroIs_Till() && (MIS_Till_Pet != LOG_SUCCESS))	{	return;	};
		
		if (MIS_Cookery != LOG_SUCCESS)	{	return;	};
		if (MIS_FellanGoHome != LOG_SUCCESS)	{	return;	};
		if (MIS_GrimbaldOrcs != LOG_SUCCESS)	{	return;	};
		if (MIS_KhorinisPost != LOG_SUCCESS)	{	return;	};	//+Пропавший работник
		if (MIS_MalethWolves != LOG_SUCCESS)	{	return;	};
		if (MIS_MalethWolves != LOG_SUCCESS)	{	return;	};
		
		if (Competition_Result != Competition_Result_NONE)	{
			B_GiveAchievement(TOPIC_Achieve_MultiQuest_Title, TOPIC_Achieve_MultiQuest_Content);
			ACHIEV_MultiQuest = TRUE;
		}
		else	{
			B_GiveAchievement(TOPIC_Achieve_Dutiful_Title, TOPIC_Achieve_Dutiful_Content);
			ACHIEV_Dutiful = TRUE;
		};
	};
};

func void B_GiveAchievement_GoalSeeking()
{
	if (!ACHIEV_GoalSeeking)	{
		if (Competition_Result == Competition_Result_WON)	{
			if (MIS_Elena_Present == LOG_SUCCESS)	{	return;	};
			if (MIS_Erol_Plates == LOG_SUCCESS)	{	return;	};
			if (MIS_Odo_Herbs == LOG_SUCCESS)	{	return;	};
			if (MIS_Odo_Straw == LOG_SUCCESS)	{	return;	};
			if (MIS_Rupert_Debts == LOG_SUCCESS)	{	return;	};
			if (MIS_Sarah_BadHabit == LOG_SUCCESS)	{	return;	};
			if (MIS_Sarah_OldFriends == LOG_SUCCESS)	{	return;	};
			if (MIS_Talbin_Hunt == LOG_SUCCESS)	{	return;	};
			if (MIS_Talbin_WhiteWolf == LOG_SUCCESS)	{	return;	};
			if (MIS_Till_Pet == LOG_SUCCESS)	{	return;	};
			
			if (MIS_Cookery == LOG_SUCCESS)	{	return;	};
			if (MIS_FellanGoHome == LOG_SUCCESS)	{	return;	};
			if (MIS_GrimbaldOrcs == LOG_SUCCESS)	{	return;	};
			if (MIS_KhorinisPost == LOG_SUCCESS)	{	return;	};
			if (MIS_MalethWolves == LOG_SUCCESS)	{	return;	};
			if ((MIS_LostBau == LOG_SUCCESS) && (MIS_KhorinisPost > 0))	{	return;	};	//нашли Вино, но отказались нести его письмо
			
			B_GiveAchievement(TOPIC_Achieve_GoalSeeking_Title, TOPIC_Achieve_GoalSeeking_Content);
			ACHIEV_GoalSeeking = TRUE;
		};
		
	};
};

func void B_GiveAchievement_SnowStudio()
{
	if (!ACHIEV_SnowStudio)	{
		ACHIEV_SnowStudio_Counter += 1;
		B_GivePlayerXP(XP_SnowStudio);
		if (ACHIEV_SnowStudio_Counter == 1)	{
			B_LogNote(TOPIC_SnowStudio, TOPIC_SnowStudio_General);
		};
		if (ACHIEV_SnowStudio_Counter == 18)	{
			B_GiveAchievement(TOPIC_Achieve_SnowStudio_Title, TOPIC_Achieve_SnowStudio_Content);
			ACHIEV_SnowStudio = TRUE;
		};
	};
};

//=========================================
func void B_GiveAchievement_ByTime()
{
	B_GiveAchievement_Sleepless();
	B_GiveAchievement_Xtheist();
	B_GiveAchievement_Degustator();
	B_GiveAchievement_Pioneer();
	B_GiveAchievement_Mushroomer();
	B_GiveAchievement_Mycologist();
	B_GiveAchievement_Till_TeaMaster();
	B_GiveAchievement_Talbin_Ranger();
	B_GiveAchievement_Talbin_Furrier();
	B_GiveAchievement_Rupert_Waster();
	B_GiveAchievement_Rupert_SwordMaster();
	B_GiveAchievement_Odo_Purveyor();
	B_GiveAchievement_Elena_Shopoholic();
	B_GiveAchievement_Erol_Decoder();
	B_GiveAchievement_Odo_Cleaner();
	B_GiveAchievement_MultiQuest_Dutiful();
	B_GiveAchievement_GoalSeeking();
	if (ACHIEV_POS_Y > 0)	{	ACHIEV_POS_Y -= 1;	};
};
